plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "com.bmuschko.docker-remote-api" version "6.1.4"
}


import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
import com.bmuschko.gradle.docker.tasks.*

docker {
    url = 'tcp://host.docker.internal:2375'
//    url = 'unix:///var/run/docker.sock'
//    url = 'tcp://127.0.0.1:2375'
//    url = 'npipe:////./pipe/docker_engine'
}

task dockerInit(type: Copy) {
    dependsOn assemble

    into("$buildDir/docker")
    from("src/docker") {
        include '**/*.dockerfile'
        include 'entrypoint.sh'
    }
    from("$buildDir/libs/") {
        include '**/*.jar'
    }
}

task dockerImageBuild(type: DockerBuildImage) {
    dependsOn dockerInit

    images.add('dongpak/addressapi:latest')
    inputDir	= file("$buildDir/docker")
    dockerFile	= file("$buildDir/docker/jdk8.dockerfile")
    remove		= true
}

group 'com.churchclerk'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    runtimeOnly 'mysql:mysql-connector-java'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('com.h2database:h2')
    testImplementation('com.google.code.gson:gson')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}